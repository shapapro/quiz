
&НаКлиенте
Процедура ЛоготипНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповещениеОЗавершение = Новый ОписаниеОповещения("ЛоготипНажатиеЗавершение", ЭтотОбъект);
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗагрузки.Диалог.Фильтр = НСтр("ru = 'Изображения'") + "|*.JPG;*.JPEG;*.JP2;*.JPG2;*.PNG;*.BMP;*.TIFF";
	ФайловаяСистемаКлиент.ЗагрузитьФайл(ОповещениеОЗавершение, ПараметрыЗагрузки);
КонецПроцедуры

&НаКлиенте
Процедура ЛоготипНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	#Если Не ВебКлиент Тогда
		Картинка = Новый Картинка(ПолучитьИзВременногоХранилища(Результат.Хранение));
		Если Картинка.Формат() = ФорматКартинки.НеизвестныйФормат Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выбрать файл с изображением.'"));
			Возврат;
		КонецЕсли;
		
		Если Картинка.РазмерФайла() > 2 * 1024 * 1024 Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выбрать файл с изображением меньше 2 Мб.'"));
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	Если ЭтоАдресВременногоХранилища(АдресЛоготипа) Тогда
		УдалитьИзВременногоХранилища(АдресЛоготипа);
	КонецЕсли;
	
	АдресЛоготипа = Результат.Хранение;
	Модифицированность = Истина;  
	ЛоготипЗадан = Истина;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЛоготипЗадан И ЭтоАдресВременногоХранилища(АдресЛоготипа) Тогда
		ТекущийОбъект.Логотип = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресЛоготипа));
	Иначе 
		ТекущийОбъект.Логотип = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Логотип = ТекущийОбъект.Логотип.Получить();
	Если Логотип <> Неопределено Тогда
		АдресЛоготипа = ПоместитьВоВременноеХранилище(Логотип, УникальныйИдентификатор);
		ЛоготипЗадан = Истина;
	КонецЕсли;
	
КонецПроцедуры
