
&НаСервереБезКонтекста
Процедура ИзменитьАктивностьРаунда(Знач Квиз, Знач Раунд) 
	
	Док = Квиз.ПолучитьОбъект();
	СтрокаРаунда = Док.Раунды.Найти(Раунд);
	СтрокаРаунда.Активность = Не СтрокаРаунда.Активность;
	Док.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьСоСписаниемБалловРаунда(Знач Квиз, Знач Раунд) 
	
	Док = Квиз.ПолучитьОбъект();
	СтрокаРаунда = Док.Раунды.Найти(Раунд);
	СтрокаРаунда.СоСписаниемБаллов = Не СтрокаРаунда.СоСписаниемБаллов;
	Док.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура КвизПриИзменении(Элемент)
	
	КвизПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура КвизПриИзмененииНаСервере()
	
	Раунды.Очистить(); 
	Раунды.Загрузить(Объект.Квиз.Раунды.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура РаундыАктивностьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Раунды.ТекущиеДанные;
	ИзменитьАктивностьРаунда(Объект.Квиз, ТекущиеДанные.Раунд);
	Если ТекущиеДанные.Активность тогда
		Объект.Раунд = ТекущиеДанные.Раунд;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаундыСоСписаниемБалловПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Раунды.ТекущиеДанные;
	ИзменитьСоСписаниемБалловРаунда(Объект.Квиз, ТекущиеДанные.Раунд);
КонецПроцедуры

&НаСервере 
Процедура ПолучитьОтветыУчастниковНаСервере()  
	
	Ответы.Очистить();
	Ответы.Загрузить(РегистрыСведений.РезультатыКвиза.ПолучитьРезультатыРаунда(Объект.Квиз, Объект.Раунд)); 
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаОтветы Тогда
		ПолучитьОтветыУчастниковНаСервере();
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаТурнирнаяТаблица Тогда
		СформироватьТурнирнуюТаблицуНаСервере();
	КонецЕсли;  
	
КонецПроцедуры

&НаСервере 
Процедура СформироватьТурнирнуюТаблицуНаСервере()  
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТурнирнаяТаблица.Команда КАК Команда,
	|	ТурнирнаяТаблица.Раунд КАК Раунд,
	|	ТурнирнаяТаблица.КоличествоБаллов КАК КоличествоБаллов
	|ИЗ
	|	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	|ГДЕ
	|	ТурнирнаяТаблица.Регистратор = &Регистратор
	|ИТОГИ
	|	СУММА(КоличествоБаллов)
	|ПО
	|	Команда";
	
	Запрос.УстановитьПараметр("Регистратор", Объект.Квиз);
	
	РезультатЗапроса = Запрос.Выполнить();
	ДЗ = РеквизитФормыВЗначение("ТурнирнаяТаблица");
	ДЗ.Строки.Очистить();
	ДЗ = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ЗначениеВРеквизитФормы(ДЗ, "ТурнирнаяТаблица");
	
КонецПроцедуры



&НаКлиенте
Процедура ЗафиксироватьОтветы(Команда)
	
	ЗафиксироватьОтветыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗафиксироватьОтветыНаСервере()
	
	РегистрыСведений.РезультатыКвиза.ЗаписатьРезультатыРаунда(Объект.Квиз, Объект.Раунд, Ответы);
	Док = Объект.Квиз.ПолучитьОбъект();
	Док.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

&НаСервере
Процедура РаундыПриАктивизацииСтрокиНаСервере(Раунд)
	
	Вопросы.Параметры.УстановитьЗначениеПараметра("Раунд", Раунд);
	
КонецПроцедуры

&НаКлиенте
Процедура РаундыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Раунды.ТекущиеДанные; 
	Если ТекущиеДанные <> Неопределено Тогда
		РаундыПриАктивизацииСтрокиНаСервере(ТекущиеДанные.Раунд);
	КонецЕсли;
	
КонецПроцедуры



















